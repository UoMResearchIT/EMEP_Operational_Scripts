;
; script for ensuring that the NAEI emission files metadata conforms with the
;  metadata expected by EMEP
;
;  Example input files available here:
;   /mnt/iusers01/support/mbessdl2/Projects/Emissions_Processing/Emission_Datasets/NAEI_2016/emissions_netcdf
;


;
;  Source variable metadata (as generated by our regridding scripts):
;
;	float indproc(time, lat, lon) ;
;		indproc:_FillValue = -9999.f ;
;		indproc:units = "tonnes/km^2/year" ;
;
;  Changes / additions needed:
;
;      species='pm25' (or 'co','sox',etc) (match according to emission file name)
;      sector=[1..13]  (match according to variable name)
;      units='mg/m2/year'
;      factor=1.0e3 (convert from tonnes/km^2/year to mg/m2/year)
;      country_ISO='GB'  (fixed value for all data)
;
;  Changes / additions needed for global attributes:
; 
;      periodicity = 'yearly'
;


begin

;; file name settings
sourcedir = "output/"
species_list = (/"voc","co","nh3","nox","sox","pm25","pmco"/)
species_list := (/"nh3","nox","sox","pm25","pmco"/)
file_end = "_emiss.nc"

;; sector settings
sect_map = True
sect_map@energyprod = 1
sect_map@domcom     = 2
sect_map@indcom     = 3
sect_map@indproc    = 4
sect_map@offshore   = 5
sect_map@solvents   = 6
sect_map@roadtrans  = 7
sect_map@othertrans = 9
sect_map@waste      = 10
sect_map@agric      = 11
sect_map@nature     = 12

;; global attribute settings
globalAtt = True
globalAtt@periodicity = "yearly"

;; variable attribute settings
varAtt = True
varAtt@country_ISO = "GB"
varAtt@units       = "mg/m2/year"



;; loop through the species list
do jj = 0,dimsizes(species_list)-1
	species = species_list(jj)

	; open data file
	FILE=addfile(sourcedir+species+file_end,"w")


	;; add global attributes to the file
	fileattdef(FILE,globalAtt)


	;; loop through all variables - process only emission variables
	;;     (as identified in the "sect_map" list above)
	variable_list := getfilevarnames(FILE)


	do ii = 0,dimsizes(variable_list)-1
		vname = variable_list(ii)
		
		if(isatt(sect_map,vname))
			print("processing variable: "+vname)
		
			vardata := FILE->$vname$
			
			;; note the species name (same for all in one file)
			varAtt@species     = species
		
			;; copy sector value
			varAtt@sector = sect_map@$vname$
		
			;; check to see what scaling factor we should be using
			currunit = vardata@units
			
			if(currunit .eq. "ug m^-2 s^-1")
				varAtt@factor      = 3.1536e4
				varAtt@oldunits    = currunit
			else if(currunit .eq. "tonnes/km^2/year")
				varAtt@factor      = 1.0e3
				varAtt@oldunits    = currunit			
			else
				print("unsupported unit: "+currunit)
				varAtt@factor      = -9999.9
				varAtt@oldunits    = currunit
			end if
			end if
		
			;; apply attributes to the file variable
			filevarattdef(FILE,vname,varAtt)
		
		else
			print("not processing variable: "+vname)
		end if

	end do ;; ii = variable_list

	delete(FILE)

end do ;; jj = species_list

end



